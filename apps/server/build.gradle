import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "com.adarshr.test-logger"
    id "io.spring.dependency-management"
    id "jacoco"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.kapt"
    id "org.jetbrains.kotlin.plugin.spring"
    id "org.springframework.boot"
}

description = String.format(
    "Server-side application based on Spring Boot %s and Java %s",
    project.property("versions.springBoot"),
    project.property("versions.java"),
)

java {
    sourceCompatibility = project.property("versions.java")
    targetCompatibility = project.property("versions.java")
}

kapt {
    keepJavacAnnotationProcessors = true
}

repositories {
    mavenCentral()
}

configurations {
    "compileOnly" {
        extendsFrom annotationProcessor
    }
    "implementation" {
        exclude group: "org.javamoney.moneta", module: "moneta-convert-ecb"
        exclude group: "org.javamoney.moneta", module: "moneta-convert-imf"
        exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
    }
    "testImplementation" {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        exclude group: "org.mockito", module: "mockito-core"
        exclude group: "org.mockito", module: "mockito-junit-jupiter"
    }
    "all" {
        exclude group: "org.apache.logging.log4j", module: "log4j-to-slf4j"
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
    }
}

dependencies {
    kapt platform(rootProject)
    kapt "org.mapstruct:mapstruct-processor"
    kapt "org.springframework:spring-context-indexer"
    kapt "org.springframework.boot:spring-boot-configuration-processor"

    compileOnly "org.mapstruct:mapstruct"

    runtimeOnly "com.github.ben-manes.caffeine:caffeine"
    runtimeOnly "org.codehaus.janino:janino"
    runtimeOnly "org.postgresql:postgresql"

    developmentOnly "org.springframework.boot:spring-boot-devtools"

    implementation platform(rootProject)
    implementation project(":apps:schema")
    implementation "de.siegmar:fastcsv"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "org.flywaydb:flyway-core"
    implementation "org.flywaydb:flyway-database-postgresql"

    implementation "org.javamoney:moneta"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-slf4j"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-undertow"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.session:spring-session-jdbc"

    testImplementation "com.ninja-squad:springmockk"
    testImplementation "com.tngtech.archunit:archunit-junit5"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-testcontainers"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:postgresql"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

jacocoTestReport {
    shouldRunAfter test
    applyJaCoCoExclusions classDirectories
}

jacocoTestCoverageVerification {
    shouldRunAfter jacocoTestReport
    applyJaCoCoExclusions classDirectories

    violationRules {
        rule {
            limit {
                minimum = 0.65
            }
        }
    }
}

check {
    dependsOn(
        test,
        jacocoTestReport,
        jacocoTestCoverageVerification,
    )
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = [
        "-Xlint:deprecation",
        "-Xlint:fallthrough",
        "-Xlint:unchecked",
        "-parameters",
    ]
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        freeCompilerArgs = [
            "-Xjsr305=strict",
            "-Xjvm-default=all",
        ]
        jvmTarget = JvmTarget.valueOf("JVM_${project.property("versions.java")}")
        javaParameters = true
    }
}

def applyJaCoCoExclusions(ConfigurableFileCollection classDirectories) {
    classDirectories.from = files(classDirectories.files.collect {
        fileTree(
            dir: it,
            exclude: [
                "com/github/arhor/simple/expense/tracker/Application.class",
                "com/github/arhor/simple/expense/tracker/**/aop/*",
                "com/github/arhor/simple/expense/tracker/**/config/*",
                "com/github/arhor/simple/expense/tracker/**/error/*",
                "com/github/arhor/simple/expense/tracker/**/exception/*",
                "com/github/arhor/simple/expense/tracker/**/model/*",
            ]
        )
    })
}
