plugins {
    id "java-library"
    id "org.jsonschema2dataclass"
    id "simple-expense-tracker.node-conventions"
}

description = "Application model described via JSON Schema, used to generate sources for the Client/Server communication"

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-databind:${property("app.version.jackson")}"
    implementation "javax.validation:validation-api:${property("app.version.javaxValidation")}"
    implementation "com.google.code.findbugs:jsr305:${property("app.version.findbugsJsr305")}"
}

jsonSchema2Pojo {
    targetVersion = project.property("app.version.java")
    targetPackage = "com.github.arhor.simple.expense.tracker.model"
    removeOldOutput = true
    includeJsr303Annotations = true
    includeJsr305Annotations = true
    includeSetters = true
    includeGetters = true
    useOptionalForGetters = true
    useBigDecimals = true
    useLongIntegers = true
    dateTimeType = "java.time.LocalDateTime"
    dateType = "java.time.LocalDate"
}

task generateJsonSchema2TypeScript(type: com.github.gradle.node.npm.task.NpmTask, dependsOn: npmInstall) {
    group = "build"
    description = "Builds d.ts files according to the provided schemas"
    workingDir.fileValue(projectDir)
    args = ["run", "build"]
}

build {
    dependsOn generateJsonSchema2TypeScript
}
