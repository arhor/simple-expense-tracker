pluginManagement {
    plugins {
        // @formatter:off
        id "com.adarshr.test-logger"            version settings["version.gradleTestLogger"]
        id "com.avast.gradle.docker-compose"    version settings["version.dockerComposeGradlePlugin"]
        id "com.github.node-gradle.node"        version settings["version.nodeGradlePlugin"]
        id "io.spring.dependency-management"    version settings["version.springDependencyManagement"]
        id "org.flywaydb.flyway"                version settings["version.flyway"]
        id "org.jsonschema2dataclass"           version settings["version.json2pojo"]
        id "org.jetbrains.kotlin.kapt"          version settings["version.kotlin"]
        id "org.jetbrains.kotlin.jvm"           version settings["version.kotlin"]
        id "org.jetbrains.kotlin.plugin.spring" version settings["version.kotlin"]
        id "org.springframework.boot"           version settings["version.springBoot"]
        // @formatter:on
    }
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url "https://repo.spring.io/release" }
    }
}

rootProject.name = "simple-expense-tracker"

include ":app-client"
include ":app-model"
include ":app-server"
include ":app-test-e2e"

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            this.properties.each { name, value ->
                if (name.startsWith "version.") {
                    version(name - "version.", value)
                }
            }
        }
    }
}
