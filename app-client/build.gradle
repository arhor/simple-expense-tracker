import com.github.gradle.node.npm.task.NpmTask
import com.github.gradle.node.npm.task.NpxTask

plugins {
    id "simple-expense-tracker.node-conventions"
}

description = String.format(
    "Client-side application based on React.js and Node.js %s",
    property("versions.node")
)

def generatedSourcesDir = "src/generated"

def clearGeneratedModel = tasks.register("clearGeneratedModel", Delete) {
    delete generatedSourcesDir
}

def copyGeneratedModel = tasks.register("copyGeneratedModel", Copy) {
    dependsOn(clearGeneratedModel, ":app-model:build")

    from "${project(":app-model").buildDir}/generated/sources/js2ts"
    into "${projectDir}/${generatedSourcesDir}"
}

def updateBrowserList = tasks.register("updateBrowserList", NpxTask) {
    dependsOn(npmInstall)

    workingDir.fileValue(projectDir)
    command.set("browserslist@latest")

    args = ["--update-db"]
}

def test = tasks.register("test", NpmTask) {
    dependsOn(updateBrowserList, copyGeneratedModel)

    group = "verification"
    workingDir.fileValue(projectDir)

    args = ["run", "test"]
}

tasks.register("build", NpmTask) {
    dependsOn(test)

    group = "build"
    workingDir.fileValue(projectDir)

    args = ["run", "build"]
}

tasks.register("serve", NpmTask) {
    dependsOn(npmInstall, copyGeneratedModel)

    group = "application"
    workingDir.fileValue(projectDir)

    args = ["run", "serve"]
}
