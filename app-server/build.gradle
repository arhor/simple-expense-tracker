plugins {
    id "com.adarshr.test-logger"
    id "io.freefair.lombok"
    id "io.spring.dependency-management"
    id "jacoco"
    id "java"
    id "org.flywaydb.flyway"
    id "org.springframework.boot"
}

description = "Server-side application based on Spring Boot %s and Java %s".formatted(
    property("version.springBoot"),
    property("version.java"),
)

java {
    sourceCompatibility = property("version.java")
    targetCompatibility = property("version.java")
}

jacoco {
    toolVersion = property("version.jacoco")
}

flyway {
    url = env.getRequired "JDBC_DATABASE_URL"
    user = env.getRequired "JDBC_DATABASE_USERNAME"
    password = env.getRequired "JDBC_DATABASE_PASSWORD"

    driver = "org.postgresql.Driver"
    encoding = "UTF-8"
    locations = ["classpath:db/migration"]
    baselineVersion = "0.0"
    baselineOnMigrate = true
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/release" }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    implementation {
        exclude module: "spring-boot-starter-tomcat"
        exclude module: "moneta-convert-ecb"
        exclude module: "moneta-convert-imf"
    }
    testImplementation {
        exclude module: "junit-vintage-engine"
    }
}

dependencies {
    annotationProcessor platform(rootProject)
    annotationProcessor "org.mapstruct:mapstruct-processor"
    annotationProcessor "org.springframework:spring-context-indexer"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    compileOnly "org.mapstruct:mapstruct"
    runtimeOnly "org.postgresql:postgresql"
    developmentOnly "org.springframework.boot:spring-boot-devtools"

    implementation platform(rootProject)
    implementation project(":app-model")
    implementation "de.siegmar:fastcsv"
    implementation "org.apache.commons:commons-lang3"
    implementation "org.apache.commons:commons-collections4"
    implementation "org.flywaydb:flyway-core"
    implementation "org.javamoney:moneta"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-undertow"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.session:spring-session-core"

    testImplementation "com.tngtech.archunit:archunit-junit5"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:postgresql"
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${property("version.testcontainers")}"
    }
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    shouldRunAfter test
    applyJaCoCoExclusions(classDirectories)
}

jacocoTestCoverageVerification {
    shouldRunAfter jacocoTestReport
    applyJaCoCoExclusions(classDirectories)

    violationRules {
        rule {
            limit {
                minimum = 0.10
            }
        }
    }
}

check {
    dependsOn = [
        test,
        jacocoTestReport,
        jacocoTestCoverageVerification,
    ]
}

void applyJaCoCoExclusions(ConfigurableFileCollection classDirectories) {
    classDirectories.from = files(classDirectories.files.collect {
        fileTree(
            dir: it,
            exclude: [
                "com/github/arhor/simple/expense/tracker/Application.class",
                "com/github/arhor/simple/expense/tracker/web/error/ErrorResponse.class",
                "com/github/arhor/simple/expense/tracker/web/error/GlobalExceptionHandler.class",
                "com/github/arhor/simple/expense/tracker/**/config/*",
                "com/github/arhor/simple/expense/tracker/**/exception/*",
                "com/github/arhor/simple/expense/tracker/**/model/*",
            ]
        )
    })
}

tasks.withType(JavaCompile) {
    options.compilerArgs = [
        "-Xlint:deprecation",
        "-Xlint:unchecked",
        "-parameters",
    ]
}
